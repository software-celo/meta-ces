# POKY_BBLAYERS_CONF_VERSION is increased each time build/conf/bblayers.conf
# changes incompatibly
POKY_BBLAYERS_CONF_VERSION = "2"

BBPATH = "${TOPDIR}"
BBFILES ?= ""

BBLAYERS_DIR = "${@os.path.abspath(os.path.join("${TOPDIR}", os.pardir))}/sources"

YOCTO_LAYERS = " \
  ${BBLAYERS_DIR}/poky/meta \
  ${BBLAYERS_DIR}/poky/meta-poky \
  "

CES_CORE_DEPENDENCY_LAYERS = " \
  ${BBLAYERS_DIR}/meta-openembedded/meta-oe \
  ${BBLAYERS_DIR}/meta-openembedded/meta-filesystems \
  ${BBLAYERS_DIR}/meta-openembedded/meta-multimedia \
  ${BBLAYERS_DIR}/meta-openembedded/meta-networking \
  ${BBLAYERS_DIR}/meta-openembedded/meta-python \
  "

CES_LAYERS = " \
  ${BBLAYERS_DIR}/meta-ces \
  "

CES_BROWSER_DEPENDENCY_LAYERS = " \
  ${BBLAYERS_DIR}/meta-clang \
  ${BBLAYERS_DIR}/meta-browser \
  ${BBLAYERS_DIR}/meta-qt5 \
  "

CES_HW_DEPENDENCY_LAYERS = " \
  ${BBLAYERS_DIR}/meta-freescale \
  "

BBLAYERS ?= " \
  ${YOCTO_LAYERS} \
  ${CES_CORE_DEPENDENCY_LAYERS} \
  ${CES_HW_DEPENDENCY_LAYERS} \
  ${CES_BROWSER_DEPENDENCY_LAYERS} \
  ${CES_LAYERS} \
  "

BBLAYERS_NON_REMOVABLE ?= " \
  ${YOCTO_LAYERS} \
  ${CES_LAYERS} \
  "

# bbappend to mesa from Freescale layer is harmful, because enabled features
# 'imxgpu' and 'imxgpu3d' filter out wanted "egl gbm gles" from PACKAGECONFIG.
# In addition after backporting fixes to mesa-19 the enabled vc4 driver is
# not needed anymore.
BBMASK_ces-fb += "${BBLAYERS_DIR}/meta-freescale/recipes-graphics/mesa/mesa_%.bbappend"
